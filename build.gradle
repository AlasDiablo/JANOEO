buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

def build_number = '69'
def isExperimental = false
version = "6.6.0.${build_number}"
if (isExperimental) {
    version += '-experimental'
}
group = 'fr.alasdiablo.janoeo.foundation'
archivesBaseName = 'Janoeo Foundation'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'snapshot', version: '20210309-1.16.5'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                janoeo_foundation {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                janoeo_foundation {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'janoeo_foundation', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                janoeo_foundation {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = 'https://www.cursemaven.com/' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.13'
    compile fileTree(dir:'lib', include:'*.jar')
    // Compatibility check with mod
    implementation fg.deobf("curse.maven:jei-238222:3308495")                       // JEI
    implementation fg.deobf("curse.maven:just-enough-resources-jer-240630:3293129") // JER
    implementation fg.deobf("curse.maven:catalogue-459701:3335979")                 // Catalogue
    implementation fg.deobf("curse.maven:infinitylib-251396:3338377")               // InfinityLib
    implementation fg.deobf("curse.maven:agricraft-225635:3341924")                 // Agricraft
    implementation fg.deobf("curse.maven:mantle-74924:3326538")                     // Mantle
    implementation fg.deobf("curse.maven:tinkers-construct-74072:3326523")          // Tinkers Construct 3
    implementation fg.deobf("curse.maven:iron-chests-228756:3105315")               // Iron Chests
}

jar {
    destinationDirectory.set(file(rootProject.getRootDir().getPath() + '/output'))
}

jar.finalizedBy('reobfJar')
